plugins {
	id "java"
	id "org.sonarqube" version "3.3"
	id "com.diffplug.spotless" version "6.1.0"
}

defaultTasks 'clean', 'spotlessApply', 'build'

ext {
	dependencyVersions = [
		jackson_version          : "2.13.1",
		aws_sdk_version          : "1.12.167",
		aws_sdk_sqs_version	     : "1.12.197",
		aws_lambda_core_version  : "1.2.1",
		aws_lambda_events_version: "3.11.0",
		aws_powertools_version   : "1.9.0",
		nimbusds_oauth_version   : "9.25",
		nimbusds_jwt_version     : "9.15.1",
		protobuf_version         : "3.19.4",
		junit                    : "5.8.2",
		mockito					 : "4.3.1",
		glassfish_version        : "3.0.3",
		powertools_version       : "1.12.0",
	]
}

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

subprojects {

	java {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
	}

	configurations {
		dynamodb
		jackson
		lambda
		sqs
		kms
		lambda_tests
		lettuce
		nimbus
		ssm
		tests
		test_runtime
		gson
		powertools
		mockito
	}

	dependencies {

		dynamodb "com.amazonaws:aws-java-sdk-dynamodb:${dependencyVersions.aws_sdk_version}",
				"software.amazon.awssdk:dynamodb-enhanced:2.17.116"

		gson "com.google.code.gson:gson:2.8.9"

		lambda "com.amazonaws:aws-lambda-java-core:${dependencyVersions.aws_lambda_core_version}",
				"com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}",
				"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_version}",
				"org.apache.httpcomponents:httpclient:4.5.13"

		sqs "com.amazonaws:aws-java-sdk-sqs:${dependencyVersions.aws_sdk_sqs_version}"

		kms "com.amazonaws:aws-java-sdk-kms:${dependencyVersions.aws_sdk_version}"

		lambda_tests "com.amazonaws:aws-lambda-java-tests:1.1.1"

		jackson "com.fasterxml.jackson.core:jackson-core:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-annotations:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${dependencyVersions.jackson_version}"

		powertools "software.amazon.lambda:powertools-logging:${dependencyVersions.powertools_version}",
				"software.amazon.lambda:powertools-metrics:${dependencyVersions.powertools_version}"

		nimbus "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
				"com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}"

		tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
				"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
				"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito}",
				"org.mockito:mockito-inline:${dependencyVersions.mockito}",
				"org.hamcrest:hamcrest:2.2"

		test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-ipv-cri-address-api"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint:unchecked"
	}
}
